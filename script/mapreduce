#!/usr/bin/env perl
use strict;
use warnings;
use lib 'lib';
use POSIX qw(setsid);
use File::Pid;
use Getopt::Long;
use Time::HiRes qw(sleep);
use MapReduce::Mapper;

my $mappers  = 1;
my $reducers = 1;
my $daemon   = 1;

GetOptions(
    'mappers|m=i'  => \$mappers,
    'reducers|r=i' => \$reducers,
    'daemon|d=i'   => \$daemon,
) or die 'Error processing command line args';

my $command = shift(@ARGV) // '';

my $pidfile = File::Pid->new({
    file => '/tmp/mapreduce.pid',
});

$pidfile->write();

if ($pidfile->running) {
    my $pid = $pidfile->pid;
    
    print STDERR "$0 running in pid $pid.\n";
    
    kill 'TERM' => $pid;
    
    sleep 0.1;
    
    while (kill 0 => $pid) {
        sleep 1;
        print STDERR "Waiting for $0 in pid $pid to exit.\n";
    }
}

if (lc($command) eq 'stop') {
    print STDERR "Stopped.\n";
    exit 0;
}

print STDERR "Starting $mappers mappers and $reducers reducers.\n";

if ($daemon) {
    chdir '/';
    setsid;

    fork and exit;
    fork and exit;

    open *STDIN,  '< /dev/null';
    open *STDOUT, '> /dev/null';
    open *STDERR, '> /dev/null';

    $pidfile->pid($$);
    $pidfile->write();
}

my $run = 1;

local $SIG{INT}  = sub { $run = 0 };
local $SIG{TERM} = sub { $run = 0 };

my @mappers  = map { MapReduce::Mapper->new(daemon => $daemon)  } 1 .. $mappers;
my @reducers = map { MapReduce::Reducer->new(daemon => $daemon) } 1 .. $reducers;

$0 = 'mr.manager';

while ($run) {
    if ($daemon) {
        sleep 1;
    }
    else {
        for my $mapper (@mappers) {
            $mapper->run();
        }
        
        for my $reducer (@reducers) {
            $reducer->run();
        }
    }
}


exit 0;

